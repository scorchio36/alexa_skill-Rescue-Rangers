@startEpisode1
  *say
    [sfx alice_open.mp3]
    
    Hi there! Good to see a fresh new face around here. I am Alice.
    You may have heard of my twin sister Alexa. She is very famous now.
    <break time="0.5s" />
    
    I work as a virtual assistant for the Animal Rescue Rangers. The Animal Rescue Rangers are a group of people determined to help animals all over the world. Whenever there is an animal in need, the Rescue Rangers will be there to save the day.
    <break time="0.3s" />
    
    In this game, you are a ranger who is training to become part of the Animal Rescue Rangers. You have been paired up with a top-notch Rescue Ranger named Olive. She is going to tell you everything you need to know. I will also be with you at all times. So if you need help at any time, you can say help.
    <break time="0.5s" />
    
    If you are ready for adventure, you can say play the game. Otherwise you can say quit.
    
  *reprompt
    You can say play the game or you can say quit.
  *then
    hear play the game, play, let's play, I'm ready for adventure {
      -> Episode1-Adventure Awaits
    }
    hear quit, I don't want to play {
    }
    
@Episode1-Adventure Awaits
  *say
    Episode 1.
    <break time="0.4s" />
    A New Adventure Awaits.
    <break time="0.7s" />
    
    Alright Ranger. I will now be teleporting you to the Animal Rescue Rangers headquarters. Hold on tight and good luck with your training. You are going to do great.
    <break time="0.5s" />
    
    //Olive introduces herself to the player. She also gives the player their Field Guide Gadget.
    [sfx olive_intro.mp3]
    Item Obtained.
    <break time="0.2s" />
    It's a Field Guide Gadget.
    
    //Olive introduces the player to Alice, the Rescue Rangers Virtual Assistant.
    [sfx olive_intro2.mp3]
    Hi again, Ranger.
    
    //Olive tells player about the FGG's Teleportation command and prompts the user to teleport to France.
    [sfx olive_intro3.mp3]
    
  *reprompt
    Say teleport to France, to try out your Field Guide Gadget's teleportation capabalities.

  *then
    set context as 'tutorial - teleport'
    //dummy_intent used to pause flow so player can call global commands
    hear dummy_intent {
    }
    
@Episode1-Player Teleports to France
  *say
    //Olive tells the player that they did not actually teleport. She then talks about the FGG's ability to give you information about any animal and prompts the player to ask the FGG to tell him/her about Giraffes.
    [sfx olive_intro4.mp3]
    
  *then
    hear dummy_intent {
    }
    
@Episode1-Animal Info about Giraffes
*say
  //Olive tells the user about the FGG's scan command and then prompts the player to user the scan command.
  [sfx olive_intro5.mp3]
  
*reprompt
  Try saying scan to use the Field Guide Gadget's scan command.
  
*then
  hear dummy_intent {
  }
  

@Episode1-Animal Alert Call
  *say
    //Olive compliments the player on using scan and then gives the player a Ranger Pack
    [sfx olive_intro6.mp3]
  
    [sfx item_found_sound.mp3]
    Item obtained. It's a ranger pack. Nice!
  
    //Olive tells the player about the Rescue Pack and then is interrupted by an Animal Alert
    [sfx olive_intro7.mp3]
  
    Right away.
    <break time="0.75s"/>
    Call is ready. <break time="0.5s"/> I just need our new ranger to say <break time="0.2s"/> confirm <break time="0.2s"/> to open the call. Say confirm if you are ready for the call ranger.
  
  *then
    hear confirm {
      -> Episode1-Player Accepts Animal Alert Call
    }
    
@Episode1-Player Accepts Animal Alert Call
  *say
    Connection opened.
    //Call with Mr. Wei. 
    [sfx olive_intro8.mp3]
    Connection terminated.
  
    //Olive tells the player that they need to get to China then prompts them to do something
    [sfx olive_intro9_wwyltd_removed.mp3]
  
  *then
    -> Episode1-Prompt Player to Teleport to China
    
@Episode1-Prompt Player to Teleport to China
  *say
    [sfx wwyltd.mp3]
    [sfx offer_help1.mp3]
  *then
    hear dummy_intent {
    }
  

@Episode1-Player Teleports to China
*say
  //Player meets Mr. Wei and he introduces the player to the pandas.
  [sfx china_speech1.mp3]
  
  The panda has been added to the field guide gadget animal log. I recommend you ask to learn more about pandas. It may be helpful for you to know more about them. You can do so by saying <break time="0.2s"/> tell me about pandas.
  
*then
  set context as 'episode1 - learning about pandas'
  hear dummy_intent {
  }

@Episode1-Animal Info about Pandas
  *say
    //Player meets the mother of the lost cub and is prompted to perform an action (scan)
    [sfx china_speech2_wwyltd_removed.mp3]
    
  *then
    set context as 'episode1 - panda enclosure scan'
    -> Episode1-Prompt Player for scan in Panda Enclosure
    
@Episode1-Prompt Player for scan in Panda Enclosure
  *say
    [sfx wwyltd.mp3]
    [sfx offer_help2.mp3]
  *then
    hear dummy_intent {
    }
    
@Episode1-Scan used in Panda Enclosure
  *say
    //Olive does a recap on the scan then asks the player what they would like to search
    [sfx searching_speech1.mp3]
    
    To search something, say search and then the thing you would like to search.
  *then
    hear tree, the tree, large tree, search the large tree, search the tree {
      -> Episode1-Panda Enclosure Player Searches Tree
    }
    hear bamboo, collection of bamboo, search the bamboo, search the collection of bamboo {
      -> Episode1-Panda Enclosure Player Searches Bamboo Collection
    }
    hear stream of water, search the water, search the stream of water, search the stream {
      -> Episode1-Panda Enclosure Player Searches Water
    }
    hear hole, large hole, search the hole, search the hole at the back of the enclosure {
      -> Episode1-Panda Enclosure Player Searches Hole
    }
    hear pandas, the pandas, search the pandas, search the group of pandas rolling around {
      -> Episode1-Panda Enclosure Player Searches Pandas
    }
    hear can you repeat that {
      -> Episode1-Panda Enclosure Scan Recap
    }
    
    
@Episode1-Panda Enclosure Scan Recap
  *say
    You can search the tree, the bamboo collection, the stream of water, the hole, or the group of pandas. What would you like to search?
  *then
    hear tree, the tree, large tree, search the large tree, search the tree {
      -> Episode1-Panda Enclosure Player Searches Tree
    }
    hear bamboo, collection of bamboo, search the bamboo, search the collection of bamboo {
      -> Episode1-Panda Enclosure Player Searches Bamboo Collection
    }
    hear stream of water, search the water, search the stream of water, search the stream {
      -> Episode1-Panda Enclosure Player Searches Water
    }
    hear hole, large hole, search the hole, search the hole at the back of the enclosure {
      -> Episode1-Panda Enclosure Player Searches Hole
    }
    hear pandas, the pandas, search the pandas, search the group of pandas rolling around {
      -> Episode1-Panda Enclosure Player Searches Pandas
    }
    hear can you repeat that {
      -> Episode1-Panda Enclosure Scan Recap
    }
    

@Episode1-Panda Enclosure Player Searches Tree
  *say
    //Olive searches the tree with the player and finds a note. She then prompts user with wwyltd
    [sfx searching_speech2.mp3]
    
  *then
    -> Episode1-Panda Enclosure Scan Recap
    
@Episode1-Panda Enclosure Player Searches Bamboo Collection
  *say
    //Olive prompts the user to run another scan within the bamboo collection
    [sfx searching_speech3.mp3]
    
  *then
    -> Episode1-Prompt Player to Scan Bamboo Collection

@Episode1-Panda Enclosure Player Searches Water
  *say
    [sfx searching_speech4.mp3]
    The Koi Fish has been added to the Animal Log Book.
    [sfx searching_speech5.mp3]
  *then
    -> Episode1-Panda Enclosure Scan Recap
    
@Episode1-Panda Enclosure Player Searches Hole
  *say
    [sfx searching_speech6.mp3]
  *then
    -> Episode1-Panda Enclosure Scan Recap
    
@Episode1-Panda Enclosure Player Searches Pandas
  *say
    [sfx searching_speech7.mp3]
  *then
    -> Episode1-Panda Enclosure Scan Recap
    
@Episode1-Scan used in Bamboo Collection
  *say
    [sfx searching_speech10_without_toys_and_wwyltd.mp3]
  *then
    -> Episode1-Prompt Player for Search in Bamboo Collection
    
    
@Episode1-Prompt Player to Scan Bamboo Collection
  *say
    What would you like to do ranger?
  *then
    set context as 'episode1 - bamboo collection'
    hear dummy_intent {
    }
    
@Episode1-Prompt Player for Search in Bamboo Collection 
  *say
    You can search the pile of bamboo or the air vent.
    [sfx wwyltd.mp3]
    [sfx offer_help1.mp3]
  *then
    hear search the bamboo, search the pile, search the bamboo pile, bamboo pile {
      -> Episode1-Player Searches Bamboo Pile
    }
    hear search the air vent, search the vent, air vent {
      -> Episode1-Player Searches Air Vent
    }
    
@Episode1-Player Searches Bamboo Pile
  *say
    //Olive talks about how Pandas love bamboo. Bamboo added to Ranger pack.
    [sfx searching_speech8.mp3]
    Item obtained. Bamboo has been added to the Ranger Pack.
    My sensors also indicate there are panda toys within the pile.
    
    //Olive talks about panda toys. Toys added to Ranger pack.
    [sfx searching_speech11.mp3]
    Item obtained. A panda toy has been added to the Ranger pack.
    [sfx searching_speech12_without_wwyltd.mp3]
  *then
    -> Episode1-Scan used in Bamboo Collection
    
@Episode1-Player Searches Air Vent
  *say
    //Olive and the player enter the air vent. She then prompts the player to go right or left.
    //Simulator breaks if there are no written words. 'Dummy Words' added to prevent this.
    Dummy Words. 
    [sfx air_vent_speech1.mp3]
  *then
    hear left {
      -> Episode1-Air Vent CP1 Player Turns Left
    }
    hear right {
      -> Episode1-Air Vent CP1 Player Turns Right
    }
    
@Episode1-Air Vent Restart
  *say
    Dummy Words.
    [sfx air_vent_restart.mp3]
  *then
    hear left {
      -> Episode1-Air Vent CP1 Player Turns Left
    }
    hear right {
      -> Episode1-Air Vent CP1 Player Turns Right
    }

//CP = Checkpoint
//CP1 = Just entered the air vents
//CP2 = Left side, 2-way split
//CP3 = Right side, 3-way split
//CP4 = Last 2-way split
@Episode1-Air Vent CP1 Player Turns Left
  *say
    Dummy Words.
    [sfx air_vent_speech2.mp3]
  *then
    hear left {
      -> Episode1-Air Vent CP2 Player Turns Left
    }
    hear right {
      -> Episode1-Air Vent CP2 Player Turns Right
    }
@Episode1-Air Vent CP1 Player Turns Right
  *say
    Dummy Words.
    [sfx air_vent_speech3.mp3]
  *then
    hear left {
      -> Episode1-Air Vent CP3 Player Turns Left
    }
    hear right {
      -> Episode1-Air Vent CP3 Player Turns Right
    }
    hear forward {
      -> Episode1-Air Vent CP3 Player Goes Forward
    }
    
@Episode1-Air Vent CP2 Player Turns Left
  *say
    Dummy Words.
    [sfx air_vent_speech4_without_restart.mp3]
  *then
    -> Episode1-Air Vent Restart
    
@Episode1-Air Vent CP2 Player Turns Right
  *say
    Dummy Words.
    [sfx air_vent_speech5_without_restart.mp3]
  *then
    -> Episode1-Air Vent Restart
    
@Episode1-Air Vent CP3 Player Turns Left
  *say
    [sfx air_vent_speech6_without_restart.mp3]
  *then
    -> Episode1-Air Vent Restart
    
    
@Episode1-Air Vent CP3 Player Turns Right
  *say
    [sfx air_vent_speech7_without_restart.mp3]
  *then
    -> Episode1-Air Vent Restart
    
@Episode1-Air Vent CP3 Player Goes Forward
  *say
    [sfx air_vent_speech8.mp3]
  *then
    hear left {
      -> Episode1-Air Vent CP4 Player Turns Left
    }
    
    hear right {
      -> Episode1-Air Vent CP4 Player Turns Right
    }
    
@Episode1-Air Vent CP4 Player Turns Right
  *say
    [sfx air_vent_speech7_without_restart.mp3]
  *then
    -> Episode1-Air Vent Restart
    
@Episode1-Air Vent CP4 Player Turns Left
  *say
    [sfx air_vent_speech9.mp3]
    [sfx badge_obtained_sound.mp3]
    You have received your level 1 puzzle solver badge for solving your first puzzle. Congratulations on getting your first Animal Rescue Ranger Badge.
    [sfx lion_speech1_without_wwyltd.mp3]
  *then
    -> Episode1-Prompt Player to Scan Outside Lion Enclosure

@Episode1-Prompt Player to Scan Outside Lion Enclosure
  *say
    [sfx wwyltd.mp3]
    [sfx offer_help2.mp3]
  *then
    set context as 'episode1 - outside lion enclosure'
    hear dummy_intent {
    }
    
@Episode1-Scan Used Outside Lion Enclosure
  *say
    [sfx lion_speech2.mp3]
    To search something, say search and then the thing you would like to search.
  *then
    hear trashcan, search the trashcan, search the trash, trash {
      -> Episode1-Outside Lion Enclosure Player Searches Trash
    }
    hear zookeeper manual, manual, search the cart {
      -> Episode1-Outside Lion Enclosure Player Searches Manual
    }
    hear bucket, search the bucket {
      -> Episode1-Outside Lion Enclosure Player Searches Bucket
    }
    hear wall, search the wall {
      -> Episode1-Outside Lion Enclosure Player Searches Wall
    }
    
@Episode1-Outside Lion Enclosure Scan Recap
  *say
    You can search the trashcan, the zookeeper manual, the wall, or the bucket.
    What would you like to search?
  *then
    hear trashcan, search the trashcan, search the trash, trash {
      -> Episode1-Outside Lion Enclosure Player Searches Trash
    }
    hear zookeeper manual, manual, search the cart {
      -> Episode1-Outside Lion Enclosure Player Searches Manual
    }
    hear bucket, search the bucket {
      -> Episode1-Outside Lion Enclosure Player Searches Bucket
    }
    hear wall, search the wall {
      -> Episode1-Outside Lion Enclosure Player Searches Wall
    }
    
@Episode1-Outside Lion Enclosure Player Searches Trash
  *say
    //Olive tells player there is nothing in the trash
    [sfx lion_search_speech3.mp3]
    [sfx badge_obtained_sound.mp3]
    You have received your level 1 curiosity badge. You are such a great explorer that you will even search through the trash for answers. Mad respect.
  *then
    -> Episode1-Outside Lion Enclosure Scan Recap
    
@Episode1-Outside Lion Enclosure Player Searches Manual
  *say
    [sfx lion_search_speech1_without_wwyltd.mp3]
  *then
    -> Episode1-Outside Lion Enclosure Scan Recap
    
@Episode1-Outside Lion Enclosure Player Searches Bucket
  *say
    //Olive tells the player about the meat in the bucket. She tells the user that she will
    //need their help distracting the lions
    Dummy Words.
    [sfx lion_search_speech2_without_listening_for_roars.mp3]
  *then
    -> Episode1-Outside Lion Enclosure Listening For Roars
    
@Episode1-Outside Lion Enclosure Player Searches Wall
  *say
    [sfx lion_search_speech5_without_wwyltd.mp3]
  *then
    -> Episode1-Outside Lion Enclosure Scan Recap
    
@Episode1-Outside Lion Enclosure Listening For Roars
  *say
    //Olive tells the players to count how many lion roars they hear
    Dummy Words.
    [sfx lion_search_speech2_listening_for_roars.mp3]
  *then
    slot numberOfRoars to 'AMAZON.NUMBER'
    hear {numberOfRoars}, {numberOfRoars} roars {
      //There are 7 roars in the sound clip
      if numberOfRoars is 7 {
        -> Episode1-Outside Lion Enclosure Player Gives Correct Count
      }
      else {
        -> Episode1-Outside Lion Enclosure Player Gives Wrong Count
      }
    }
    
@Episode1-Outside Lion Enclosure Player Gives Correct Count
  *say
    //Olive throws the correct number of pieces over the wall. The lions are distracted and the player sneaks in through the gate to get to the tree where the panda is. The panda won't come down so they need to take something from the ranger pack.
    Dummy Words.
    [sfx lion_tree_speech1_no_food_question.mp3]
    
  *then
    -> Episode1-Lion Enclosure Ask Player What to Feed Panda
    
@Episode1-Outside Lion Enclosure Player Gives Wrong Count
  *say
    Dummy Words.
    [sfx lion_tree_speech2_wrong_count_given.mp3]
  *then
    -> Episode1-Outside Lion Enclosure Listening For Roars
    
@Episode1-Lion Enclosure Ask Player What to Feed Panda
  *say
    [sfx lion_tree_speech1_food_question.mp3]
  *then
    hear watermelon {
      -> Episode1-Lion Enclosure Player Guesses Watermelon
    }
    hear slice of cheese, cheese {
      -> Episode1-Lion Enclosure Player Guesses Cheese
    }
    hear bamboo, piece of bamboo {
      -> Episode1-Lion Enclosure Player Guesses Bamboo
    }
    hear mushroom {
      -> Episode1-Lion Enclosure Player Guesses Mushroom
    }
    
@Episode1-Lion Enclosure Player Guesses Watermelon
  *say
    Dummy Words.
    [sfx lion_tree_speech4_without_question.mp3]
  *then
    -> Episode1-Lion Enclosure Ask Player What to Feed Panda
    
@Episode1-Lion Enclosure Player Guesses Cheese
  *say
    Dummy Words.
    [sfx lion_tree_speech5_without_question.mp3]
  *then
    -> Episode1-Lion Enclosure Ask Player What to Feed Panda
    
@Episode1-Lion Enclosure Player Guesses Bamboo
  *say
    Dummy Words.
    [sfx lion_tree_speech3_fixed.mp3]
    To activate the rope, just say activate rope. Hurry Ranger.
  *then
    hear activate rope, rope, activate ranger pack rope, activate ranger pack {
      -> Episode1-Lion Enclosure Player Activates Rope
    }
@Episode1-Lion Enclosure Player Guesses Mushroom
  *say
    Dummy Words.
    [sfx lion_tree_speech6_without_question.mp3]
  *then
    -> Episode1-Lion Enclosure Ask Player What to Feed Panda

@Episode1-Lion Enclosure Player Activates Rope
  *say
    [sfx alice_open.mp3]
    Activating rescue pack.
    [sfx activate_rescue_pack.mp3]
    Launching rope.
    [sfx rope_launch_sound.mp3]
    Rope is secure and ready to climb, Ranger.
    [sfx climb_speech3_without_wwyltd.mp3]
  *then
    -> Episode1-Lion Enclosure Player is Prompted to Climb
    
@Episode1-Lion Enclosure Player is Prompted to Climb
  *say
    [sfx wwyltd.mp3]
    [sfx offer_help1.mp3]
  *then
    hear climb, climb the rope, climb up, use the rope {
    }