@startEpisode1
  *say
    [sfx alice_open.mp3]
    
    Hi there! Good to see a fresh new face around here. I am Alice.
    You may have heard of my twin sister Alexa. She is very famous now.
    <break time="0.5s" />
    
    I work as a virtual assistant for the Animal Rescue Rangers. The Animal Rescue Rangers are a group of people determined to help animals all over the world. Whenever there is an animal in need, the Rescue Rangers will be there to save the day.
    <break time="0.3s" />
    
    In this game, you are a ranger who is training to become part of the Animal Rescue Rangers. You have been paired up with a top-notch Rescue Ranger named Olive. She is going to tell you everything you need to know. I will also be with you at all times. So if you need help at any time, you can say help.
    <break time="0.5s" />
  *then
    set context as 'Episode1 - Play the Game'
    -> Episode1-Prompt Player to Play the Game
   
   
@Episode1-Prompt Player to Play the Game
  *say
    If you are ready for adventure, you can say play the game. Otherwise you can say quit.
    *reprompt
    You can say play the game or you can say quit.
  *recap
    You can say play the game or you can say quit.
  *then
    hear play the game, play, let's play, I'm ready for adventure, let's do this, start the game, begin the episode, begin the game {
      -> Episode1-Adventure Awaits
    }
    //Make quit/exit leave the episode a global action
    
@Episode1-Adventure Awaits
  *say
    //[sfx alice_open.mp3]
    Episode 1.
    <break time="0.4s" />
    A New Adventure Awaits.
    [sfx new_episode_intro.mp3]
    <break time="0.7s" />
  *then
    -> Episode1-Alice Teleports Player to Headquarters
    
@Episode1-Alice Teleports Player to Headquarters
  *say
    Alright Ranger. I will now be teleporting you to the Animal Rescue Rangers headquarters. Hold on tight and good luck with your training. You are going to do great.
    <break time="0.5s" />
  *then
    -> Episode1-Meet Olive
    
@Episode1-Meet Olive
  *say
    //Olive introduces herself to the player. She also gives the player their Field Guide Gadget.
    [sfx olive_intro.mp3]
  *then
    -> Episode1-Player gets Field Guide Gadget
    
@Episode1-Player gets Field Guide Gadget
  *say
    Item Obtained.
    <break time="0.2s" />
    It's a Field Guide Gadget.

    //Olive introduces the player to Alice, the Rescue Rangers Virtual Assistant.
    [sfx olive_intro2.mp3]
    [sfx alice_open.mp3]
    Hi again, Ranger.

  *then
    set context as 'Tutorial - Teleport'
    -> Episode1-Player is Prompted to Teleport to France
  
@Episode1-Player is Prompted to Teleport to France  
  *say
    //Olive tells player about the FGG's Teleportation command and prompts the user to teleport to France.
    [sfx olive_intro3.mp3]
    //{empty_string}
  *reprompt
    We want to try out your Field Guide Gadget's teleportation abilities. Say teleport to France.
  *recap
    We want to try out your Field Guide Gadget's teleportation abilities. Say teleport to France.
  *then
    //dummy_intent used to pause flow so player can call global commands
    hear dummyIntent {
    }
    
@Episode1-Player Teleports to France
  *say
    //Olive tells the player that they did not actually teleport. She then talks about the FGG's ability to give you information about any animal and prompts the player to ask the FGG to tell him/her about Giraffes.
    [sfx olive_intro4.mp3]
    //{empty_string}

  *reprompt
    We want to try out your Field Guide Gadget's info abilities. Say tell me about Giraffes.
  *recap
    We want to try out your Field Guide Gadget's info abilities. Say tell me about Giraffes.
  *then
    set context as 'Tutorial - Animal Info'
    hear dummyIntent {
    }
    
@Episode1-Animal Info about Giraffes
  *say
  //Olive tells the user about the FGG's scan command and then prompts the player to user the scan command.
  [sfx olive_intro5.mp3]
  //{empty_string}
  
*reprompt
    We want to try out your Field Guide Gadget's scan abilities. Try saying scan.
  *recap
    We want to try out your Field Guide Gadget's scan abilities. Try saying scan.
  *then
    set context as 'Tutorial - Scan'
    hear dummyIntent {
    }
  
@Episode1-Ranger Pack Obtained and Call Interrupts Tutorial
  *say
    //Olive compliments the player on using scan and then gives the player a Ranger Pack
    [sfx olive_intro6.mp3]
  
    [sfx item_found_sound.mp3]
    Item obtained. It's a ranger pack. Nice!
  *then
    set context as 'Episode1 - Animal Alert'
    -> Episode1-Training Interrupted
    
@Episode1-Training Interrupted
  *say
    //Olive tells the player about the Ranger Pack and then is interrupted by an Animal Alert
    [sfx olive_intro7.mp3]
  
    Right away.
    <break time="0.75s"/>
  *then
    -> Episode1-Animal Alert Call
    
@Episode1-Animal Alert Call
  *say
    Call is ready. <break time="0.5s"/> I just need our new ranger to say <break time="0.2s"/> confirm <break time="0.2s"/> to open the call. Say confirm if you are ready for the call ranger.
  *reprompt
    There's an emergency call on the line! Say confirm to accept the call.
  *recap
    There's an emergency call on the line! Say confirm to accept the call.
  *then
    hear confirm, accept call, open call, confirm the call, answer the phone, answer the call, answer, accept, open {
      -> Episode1-Player Accepts Animal Alert Call
    }
    
@Episode1-Player Accepts Animal Alert Call
  *say
    Connection opened.
    //Call with Mr. Wei. 
    [sfx olive_intro8.mp3]
    Connection terminated.
    
  *then
    -> Episode1-Player is Prompted to Teleport to China

@Episode1-Player is Prompted to Teleport to China
  *say
    //Olive tells the player that they need to get to China then prompts them to do something
    [sfx olive_intro9_wwyltd_removed.mp3]
  *then
    set context as 'Episode1 - Headquarters'
    -> Episode1-Prompt Player to Teleport to China
    
@Episode1-Prompt Player to Teleport to China
  *say
    [sfx wwyltd.mp3]
    [sfx offer_help1.mp3]
  *reprompt
    We need to get to China, fast! Maybe we can teleport there? What would you like to do?
  *recap
    We need to get to China, fast! Maybe we can teleport there? What would you like to do?
  *then
    hear dummyIntent {
    }
  

@Episode1-Player Teleports to China
  *say
    //Player meets Mr. Wei and he introduces the player to the pandas.
    [sfx china_speech1.mp3]
    //{empty_string}
  *then
    set context as 'Episode1 - Arrival in China'
    -> Episode1-Player is Prompted to Ask About Pandas

@Episode1-Player is Prompted to Ask About Pandas
  *say
    The panda has been added to the field guide gadget animal log. I recommend you ask to learn more about pandas. It may be helpful for you to know more about them. You can do so by saying <break time="0.2s"/> tell me about pandas.
  *reprompt
    I recommend you learn more about the Panda Bear before moving on with the mission. You can do so by saying tell me about pandas.
  *recap
    I recommend you learn more about the Panda Bear before moving on with the mission. You can do so by saying tell me about pandas.
  *then
    hear dummyIntent {
    }

@Episode1-Animal Info about Pandas
  *say
    //Player meets the mother of the lost cub and is prompted to perform an action (scan)
    [sfx china_speech2_wwyltd_removed.mp3]
    
  *then
    set context as 'Episode1 - Panda Enclosure Scan Prompt'
    -> Episode1-Prompt Player for scan in Panda Enclosure
    
@Episode1-Prompt Player for scan in Panda Enclosure
  *say
    [sfx wwyltd.mp3]
    [sfx offer_help2.mp3]

  *then
    hear dummyIntent {
    }
    
@Episode1-Scan used in Panda Enclosure
  *say
    //Olive does a recap on the scan then asks the player what they would like to search
    [sfx searching_speech1.mp3]
    
    To search something, say <break time="0.2s" />search <break time="0.5s" /> followed by the thing you would like to search.
  *then
    set context as 'Episode1 - Panda Enclosure'
    hear tree, the tree, large tree, search the large tree, search the tree, look at the tree, check out the tree, go to the tree, walk over to the tree, go over to the tree {
      -> Episode1-Panda Enclosure Player Searches Tree
    }
    hear bamboo, collection of bamboo, search the bamboo, search the collection of bamboo, search bamboo, bamboo forest, go into the bamboo forest, look at the bamboo, check out the bamboo, go over to the bamboo collection {
      -> Episode1-Panda Enclosure Player Searches Bamboo Collection
    }
    hear stream of water, search the water, search the stream of water, search the stream, check out the stream, look inside the stream, river, lake, flowing water, check out the river, check out the creek , creek, look into the water, go to stream of water, go to the water, go over to the stream of water {
      -> Episode1-Panda Enclosure Player Searches Water
    }
    hear hole, large hole, search the hole, search the hole at the back of the enclosure, go to the hole, go over to the hole, check out the hole, look into the hole, look at hole, what's in the hole {
      flag treasure_dragon_statue
      flag episode1_dragon_statue
      -> Episode1-Panda Enclosure Player Searches Hole
    }
    hear pandas, the pandas, search the pandas, search the group of pandas rolling around, check out the pandas, check out the group of pandas, go over to the pandas, go over to the group of pandas, search the group of pandas, play with the pandas, say hi to the pandas, search panda bears, search group of panda bears, look at the panda bears rolling around, look at the bears, search the group of bears {
      -> Episode1-Panda Enclosure Player Searches Pandas
    }
    hear can you repeat that, what did you say, can you repeat the list, can you repeat what I can search, can you say that again {
      -> Episode1-Panda Enclosure Scan Recap
    }
    
@Episode1-Dragon Statue Already Found
  *say
    We already found a dragon statue in the hole. There was nothing left inside. Let's search something else.
  *then
    -> Episode1-Panda Enclosure Scan Recap
    
@Episode1-Panda Enclosure Scan Recap
  *say
    You can search the tree <break time="0.3s" />, the bamboo collection <break time="0.3s" />, the stream of water <break time="0.3s" />, the hole <break time="0.3s" />, or the group of pandas<break time="0.3s" />. What would you like to search?
  *then
    hear tree, the tree, large tree, search the large tree, search the tree, look at the tree, check out the tree, go to the tree, walk over to the tree, go over to the tree {
      -> Episode1-Panda Enclosure Player Searches Tree
    }
    hear bamboo, collection of bamboo, search the bamboo, search the collection of bamboo, search bamboo, bamboo forest, go into the bamboo forest, look at the bamboo, check out the bamboo, go over to the bamboo collection {
      -> Episode1-Panda Enclosure Player Searches Bamboo Collection
    }
    hear stream of water, search the water, search the stream of water, search the stream, check out the stream, look inside the stream, river, lake, flowing water, check out the river, check out the creek , creek, look into the water, go to stream of water, go to the water, go over to the stream of water {
      -> Episode1-Panda Enclosure Player Searches Water
    }
    hear hole, large hole, search the hole, search the hole at the back of the enclosure, go to the hole, go over to the hole, check out the hole, look into the hole, look at hole, what's in the hole {
      if episode1_dragon_statue {
        -> Episode1-Dragon Statue Already Found
      }
      else {
        flag episode1_dragon_statue
        -> Episode1-Panda Enclosure Player Searches Hole
      }
    }
    hear pandas, the pandas, search the pandas, search the group of pandas rolling around, check out the pandas, check out the group of pandas, go over to the pandas, go over to the group of pandas, search the group of pandas, play with the pandas, say hi to the pandas, search panda bears, search group of panda bears, look at the panda bears rolling around, look at the bears, search the group of bears {
      -> Episode1-Panda Enclosure Player Searches Pandas
    }
    hear can you repeat that, what did you say, can you repeat the list, can you repeat what I can search, can you say that again {
      -> Episode1-Panda Enclosure Scan Recap
    }
    

@Episode1-Panda Enclosure Player Searches Tree
  *say
    //Olive searches the tree with the player and finds a note. She then prompts user with wwyltd
    [sfx searching_speech2.mp3]
    //{empty_string}
  *then
    -> Episode1-Panda Enclosure Scan Recap
    
@Episode1-Panda Enclosure Player Searches Bamboo Collection
  *say
    //Olive prompts the user to run another scan within the bamboo collection
    [sfx searching_speech3.mp3]
    //{empty_string}
  *then
    set context as 'Episode1 - Bamboo Collection Scan Prompt'
    -> Episode1-Prompt Player to Scan Bamboo Collection

@Episode1-Panda Enclosure Player Searches Water
  *say
    [sfx searching_speech4.mp3]
    The Koi Fish has been added to the Animal Log Book.
    [sfx searching_speech5.mp3]
    //{empty_string}
  *then
    -> Episode1-Panda Enclosure Scan Recap
    
@Episode1-Panda Enclosure Player Searches Hole
  *say
    [sfx searching_speech6.mp3]
    //{empty_string}
  *then
    -> Episode1-Panda Enclosure Scan Recap
    
@Episode1-Panda Enclosure Player Searches Pandas
  *say
    [sfx searching_speech7.mp3]
    //{empty_string}
  *then
    -> Episode1-Panda Enclosure Scan Recap
    
@Episode1-Scan used in Bamboo Collection
  *say
    [sfx searching_speech10_without_toys_and_wwyltd.mp3]
    //{empty_string}
  *then
    set context as 'Episode1 - Bamboo Collection'
    -> Episode1-Prompt Player for Search in Bamboo Collection
    
    
@Episode1-Prompt Player to Scan Bamboo Collection
  *say
    What would you like to do ranger?
    
  *reprompt
    You are in a new area. Try running another scan.
    
  *recap
    You are in a new area. Try running another scan.
    
  *then
    
    hear dummyIntent {
    }

    
@Episode1-Prompt Player for Search in Bamboo Collection 
  *say
    You can search the pile of bamboo or the air vent.
    [sfx wwyltd.mp3]
    [sfx offer_help1.mp3]
    
  *reprompt
    Try searching the bamboo or the air vent.
    
  *recap
    Try searching the bamboo or the air vent.
    
  *then
    hear search the bamboo, search the pile, search the bamboo pile, bamboo pile, check out the bamboo, check out the bamboo pile, look at the bamboo, look into the bamboo pile, look at the pile of bamboo, check out the pile of bamboo {
      -> Episode1-Player Searches Bamboo Pile
    }
    hear search the air vent, search the vent, air vent, check out the air vent, look at the air vent, go over to the air vent, what's at the air vent, what's in the air vent {
      -> Episode1-Player Searches Air Vent
    }
    
@Episode1-Player Searches Bamboo Pile
  *say
    //Olive talks about how Pandas love bamboo. Bamboo added to Ranger pack.
    [sfx searching_speech8.mp3]
    Item obtained. Bamboo has been added to the Ranger Pack.
    My sensors also indicate there are panda toys within the pile.
    
    //Olive talks about panda toys. Toys added to Ranger pack.
    [sfx searching_speech11.mp3]
    Item obtained. A panda toy has been added to the Ranger pack.
    [sfx searching_speech12_without_wwyltd.mp3]
  *then
    -> Episode1-Prompt Player for Search in Bamboo Collection
    
@Episode1-Player Searches Air Vent
  *say
    //Olive and the player enter the air vent. She then prompts the player to go right or left.
    [sfx air_vent_speech1.mp3]
    //{empty_string}
    
  *reprompt
    You are in a dark maze. Say, left, or say, right, to tell Olive which way you want to go.
    
  *recap
    You are in a dark maze. Say, left, or say, right, to tell Olive which way you want to go.
    
  *then
    set context as 'Episode1 - Air Vent'
    flag airVentCP1
    hear left, go left, try left, check out the left, go to the left, let's go left, the left path, I want to go left, let's try left, turn left {
      -> Episode1-Air Vent CP1 Player Turns Left
    }
    hear right, go right, try right, check out the right, go to the right, let's go right, the right path, I want to go right, let's try right, turn right {
      -> Episode1-Air Vent CP1 Player Turns Right
    }
    
// There will be a continuity issue if the player uses scan inside the air vent
// at CP1. This Scene is just here to compensate for that.
@Episode1-Player uses Scan Inside Air Vent at CP1
  *say
    Would you like to go left or right?
  *then
    hear left, go left, try left, check out the left, go to the left, let's go left, the left path, I want to go left, let's try left, turn left {
      -> Episode1-Air Vent CP1 Player Turns Left
    }
    hear right, go right, try right, check out the right, go to the right, let's go right, the right path, I want to go right, let's try right, turn right {
      -> Episode1-Air Vent CP1 Player Turns Right
    }
    
@Episode1-Air Vent Restart
  *say
    [sfx air_vent_restart.mp3]
    //{empty_string}
  *then
    flag airVentCP1
    hear left, go left, try left, check out the left, go to the left, let's go left, the left path, I want to go left, let's try left, turn left {
      -> Episode1-Air Vent CP1 Player Turns Left
    }
    hear right, go right, try right, check out the right, go to the right, let's go right, the right path, I want to go right, let's try right, turn right {
      -> Episode1-Air Vent CP1 Player Turns Right
    }

//CP = Checkpoint
//CP1 = Just entered the air vents
//CP2 = Left side, 2-way split
//CP3 = Right side, 3-way split
//CP4 = Last 2-way split
@Episode1-Air Vent CP1 Player Turns Left
  *say
    [sfx air_vent_speech2.mp3]
    //{empty_string}
    
  *reprompt
    Pick what direction you would like to travel in. Left or Right.
  *recap
    Pick what direction you would like to travel in. Left or Right.
    
  *then
    unflag airVentCP1
    hear left, go left, try left, check out the left, go to the left, let's go left, the left path, I want to go left, let's try left, turn left {
      if episode1_chinese_yoyo {
        -> Episode1-Chinese Yoyo Already Found
      }
      else {
        flag treasure_chinese_yoyo
        flag episode1_chinese_yoyo
        -> Episode1-Air Vent CP2 Player Turns Left
      }
    }
    hear right, go right, try right, check out the right, go to the right, let's go right, the right path, I want to go right, let's try right, turn right {
      -> Episode1-Air Vent CP2 Player Turns Right
    }
    
@Episode1-Chinese Yoyo Already Found
  *say
    We have already headed left from here and found the chinese yo-yo treasure. Let's restart the maze.
  *then
    -> Episode1-Air Vent Restart
    
@Episode1-Air Vent CP1 Player Turns Right
  *say
    [sfx air_vent_speech3.mp3]
    //{empty_string}
    
  *reprompt
    Pick what direction you would like to travel in. Left, Right, or Forward.
  *recap
    Pick what direction you would like to travel in. Left, Right, or Forward.
    
  *then
    unflag airVentCP1
    hear left, go left, try left, check out the left, go to the left, let's go left, the left path, I want to go left, let's try left, turn left {
      -> Episode1-Air Vent CP3 Player Turns Left
    }
    hear right, go right, try right, check out the right, go to the right, let's go right, the right path, I want to go right, let's try right, turn right {
      -> Episode1-Air Vent CP3 Player Turns Right
    }
    hear forward, go forward, try forward, check out the forward, go to the forward, let's go forward, the forward path, I want to go forward, let's try forward, turn forward, go straight, head straight {
      -> Episode1-Air Vent CP3 Player Goes Forward
    }
    
@Episode1-Air Vent CP2 Player Turns Left
  *say
    [sfx air_vent_speech4_without_restart.mp3]
    //{empty_string}
    
  *reprompt
    Pick what direction you would like to travel in. Left or Right.
  *recap
    Pick what direction you would like to travel in. Left or Right.
    
  *then
    -> Episode1-Air Vent Restart
    
@Episode1-Air Vent CP2 Player Turns Right
  *say
    [sfx air_vent_speech5_without_restart.mp3]
    //{empty_string}
    
  *reprompt
    Pick what direction you would like to travel in. Left or Right.
  *recap
    Pick what direction you would like to travel in. Left or Right.
    
  *then
    -> Episode1-Air Vent Restart
    
@Episode1-Air Vent CP3 Player Turns Left
  *say
    [sfx air_vent_speech6_without_restart.mp3]
    //{empty_string}
  *reprompt
    Pick what direction you would like to travel in. Left or Right.
  *recap
    Pick what direction you would like to travel in. Left or Right.
    
  *then
    -> Episode1-Air Vent Restart
    
    
@Episode1-Air Vent CP3 Player Turns Right
  *say
    [sfx air_vent_speech7_without_restart.mp3]
    //{empty_string}
  
  *reprompt
    Pick what direction you would like to travel in. Left or Right.
  *recap
    Pick what direction you would like to travel in. Left or Right.
    
  *then
    -> Episode1-Air Vent Restart
    
@Episode1-Air Vent CP3 Player Goes Forward
  *say
    [sfx air_vent_speech8.mp3]
    //{empty_string}
    
  *reprompt
    Pick what direction you would like to travel in. Left or Right.
  *recap
    Pick what direction you would like to travel in. Left or Right.
    
  *then
    hear left, go left, try left, check out the left, go to the left, let's go left, the left path, I want to go left, let's try left, turn left {
      -> Episode1-Air Vent CP4 Player Turns Left
    }
    
    hear right, go right, try right, check out the right, go to the right, let's go right, the right path, I want to go right, let's try right, turn right {
      -> Episode1-Air Vent CP4 Player Turns Right
    }
    
@Episode1-Air Vent CP4 Player Turns Right
  *say
    [sfx air_vent_speech7_without_restart.mp3]
    //{empty_string}
    
  *reprompt
    Pick what direction you would like to travel in. Left or Right.
  *recap
    Pick what direction you would like to travel in. Left or Right.
    
  *then
    -> Episode1-Air Vent Restart
    
@Episode1-Air Vent CP4 Player Turns Left
  *say
    [sfx air_vent_speech9.mp3]
    You have received your level 1 puzzle solver badge for solving your first puzzle. Congratulations on getting your first Animal Rescue Ranger Badge.
    [sfx lion_speech1_without_wwyltd.mp3]
    The Lion has been added to the Animal Log Book.
  *then
    flag badge_level_1_puzzle_solver
    set context as 'Episode1 - Outside Lion Enclosure Scan Prompt'
    -> Episode1-Prompt Player to Scan Outside Lion Enclosure

@Episode1-Prompt Player to Scan Outside Lion Enclosure
  *say
    [sfx wwyltd.mp3]
    [sfx offer_help2.mp3]
  *then
    hear dummyIntent {
    }
    
@Episode1-Scan Used Outside Lion Enclosure
  *say
    [sfx lion_speech2.mp3]
    To search something, say search and then the thing you would like to search.
  *then
    set context as 'Episode1 - Outside Lion Enclosure'
    hear trashcan, search the trashcan, search the trash, trash, go to the trashcan, search the trash can, check out the trash, check out the trash can, check out the trashcan, what's in the trashcan, look in the trash, look at the trash, search the garbage can, search the garbage, look in the trash bin, search the trash bin, check out the trash bin {
      -> Episode1-Outside Lion Enclosure Player Searches Trash
    }
    hear zookeeper manual, manual, search the cart, check out the manual, check out the zookeeper book, check out the zookeeper manual, search the manual, search the zookeeper cart, search the zookeeper book, search the book, look at the zookeeper book, look at the zookeeper manual, what's in the zoopkeeper manual, what's on the cart {
      -> Episode1-Outside Lion Enclosure Player Searches Manual
    }
    hear bucket, search the bucket, what's in the bucket, look inside the bucket, look at the bucket, check out the bucket, check the bucket, look at bucket {
      -> Episode1-Outside Lion Enclosure Player Searches Bucket
    }
    hear wall, search the wall, look at the wall, search wall, check out the wall, go over to the wall, look at wall  {
      -> Episode1-Outside Lion Enclosure Player Searches Wall
    }
    hear can you repeat that, what did you say, can you repeat the list, can you repeat what I can search, can you say that again {
      -> Episode1-Outside Lion Enclosure Scan Recap
    }
    
@Episode1-Outside Lion Enclosure Scan Recap
  *say
    You can search the trashcan <break time="0.3s" />, the zookeeper manual <break time="0.3s" />, the wall <break time="0.3s" />, or the bucket <break time="0.3s" />.
    What would you like to search?
  *then
    hear trashcan, search the trashcan, search the trash, trash, go to the trashcan, search the trash can, check out the trash, check out the trash can, check out the trashcan, what's in the trashcan, look in the trash, look at the trash, search the garbage can, search the garbage, look in the trash bin, search the trash bin, check out the trash bin {
      -> Episode1-Outside Lion Enclosure Player Searches Trash
    }
    hear zookeeper manual, manual, search the cart, check out the manual, check out the zookeeper book, check out the zookeeper manual, search the manual, search the zookeeper cart, search the zookeeper book, search the book, look at the zookeeper book, look at the zookeeper manual, what's in the zoopkeeper manual, what's on the cart {
      -> Episode1-Outside Lion Enclosure Player Searches Manual
    }
    hear bucket, search the bucket, what's in the bucket, look inside the bucket, look at the bucket, check out the bucket, check the bucket, look at bucket {
      -> Episode1-Outside Lion Enclosure Player Searches Bucket
    }
    hear wall, search the wall, look at the wall, search wall, check out the wall, go over to the wall, look at wall {
      -> Episode1-Outside Lion Enclosure Player Searches Wall
    }
    hear can you repeat that, what did you say, can you repeat the list, can you repeat what I can search, can you say that again {
      -> Episode1-Outside Lion Enclosure Scan Recap
    }
    
@Episode1-Outside Lion Enclosure Player Searches Trash
  *say
    //Olive tells player there is nothing in the trash
    [sfx lion_search_speech3.mp3]
    [sfx badge_obtained_sound.mp3]
    You have received your level 1 curiosity badge. You are such a great explorer that you will even search through the trash for answers. Mad respect.
  *then
    flag badge_level_1_curiosity
    -> Episode1-Outside Lion Enclosure Scan Recap
    
@Episode1-Outside Lion Enclosure Player Searches Manual
  *say
    [sfx lion_search_speech1_without_wwyltd.mp3]
    //{empty_string}
  *then
    -> Episode1-Outside Lion Enclosure Scan Recap
    
@Episode1-Outside Lion Enclosure Player Searches Bucket
  *say
    //Olive tells the player about the meat in the bucket. She tells the user that she will
    //need their help distracting the lions
    [sfx lion_search_speech2_without_listening_for_roars.mp3]
    //{empty_string}
  *then
    set context as 'Episode1 - Lion Enclosure Listening For Roars'
    -> Episode1-Outside Lion Enclosure Listening For Roars
    
@Episode1-Outside Lion Enclosure Player Searches Wall
  *say
    [sfx lion_search_speech5_without_wwyltd.mp3]
    //{empty_string}
  *then
    -> Episode1-Outside Lion Enclosure Scan Recap
    
@Episode1-Outside Lion Enclosure Listening For Roars
  *say
    //Olive tells the players to count how many lion roars they hear
    [sfx lion_search_speech2_listening_for_roars.mp3]
    //{empty_string}
    
  *reprompt
    Choose the number of pieces of meat that Olive should throw over the wall to distract the lions. It should be the same number as the number of lion roars that you hear.
  *recap
    Choose the number of pieces of meat that Olive should throw over the wall to distract the lions. It should be the same number as the number of lion roars that you hear.
  *then
    slot numberOfRoars to 'AMAZON.NUMBER'
    hear {numberOfRoars}, {numberOfRoars} roars, I heard {numberOfRoars}, I heard {numberOfRoars} lion roars {
      //There are 7 roars in the sound clip
      if numberOfRoars is 7 || lion_challenge_fails_count is 2 {
        set context as 'Episode1 - Lion Enclosure'
        -> Episode1-Outside Lion Enclosure Player Gives Correct Count
      }
      else {
        increase lion_challenge_fails_count by 1
        -> Episode1-Outside Lion Enclosure Player Gives Wrong Count
      }
    }
    hear dummyIntent {
    }
    
@Episode1-Outside Lion Enclosure Player Gives Correct Count
  *say
    //Olive throws the correct number of pieces over the wall. The lions are distracted and the player sneaks in through the gate to get to the tree where the panda is. The panda won't come down so they need to take something from the ranger pack.
    [sfx lion_tree_speech1_no_food_question.mp3]
    //{empty_string}
    
  *then
    -> Episode1-Lion Enclosure Ask Player What to Feed Panda
    
@Episode1-Outside Lion Enclosure Player Gives Wrong Count
  *say
    [sfx lion_tree_speech2_wrong_count_given.mp3]
    //{empty_string}
  *then
    -> Episode1-Outside Lion Enclosure Listening For Roars
    
@Episode1-Lion Enclosure Ask Player What to Feed Panda
  *say
    [sfx lion_tree_speech1_food_question.mp3]
    //{empty_string}
  
  *reprompt
    The baby panda is hungry. Should we feed him a watermelon, a piece of cheese, a piece of bamboo, or a mushroom.
  *recap
    The baby panda is hungry. Should we feed him a watermelon, a piece of cheese, a piece of bamboo, or a mushroom.
  *then
    hear watermelon, feed him a watermelon, give him watermelon, let's try the watermelon, see if he likes the watermelon, how about watermelon, feed him watermelon, give the panda a watermelon, try giving the baby panda some watermelon, feed the baby panda watermelon {
      -> Episode1-Lion Enclosure Player Guesses Watermelon
    }
    hear slice of cheese, cheese, give him a slice of cheese, let's try the cheese, let's try a slice of cheese, see if he likes cheese, see if he would like some cheese, give him some cheese, how about the cheese, how about a slice of cheese, feed him cheese, feed him a slice of cheese, feed him the cheese, feed the baby panda cheese, feed the baby panda a slice of cheese, give the panda some cheese {
      -> Episode1-Lion Enclosure Player Guesses Cheese
    }
    hear bamboo, piece of bamboo, try the bamboo, give him a piece of bamboo, give him bamboo, feed him a piece of bamboo, feed him bamboo, feed the panda some bamboo, feed the panda bear some bamboo, see if he likes bamboo, how about bamboo, how about a slice of bamboo {
      -> Episode1-Lion Enclosure Player Guesses Bamboo
    }
    hear mushroom, feed him a mushroom, feed him mushroom, how about the mushroom, let's try the mushroom, try feeding him mushroom, give him mushroom, feed the baby panda some mushroom, see if he likes mushroom, try the mushroom {
      -> Episode1-Lion Enclosure Player Guesses Mushroom
    }
    
@Episode1-Lion Enclosure Player Guesses Watermelon
  *say
    [sfx lion_tree_speech4_without_question.mp3]
    //{empty_string}
  *then
    -> Episode1-Lion Enclosure Ask Player What to Feed Panda
    
@Episode1-Lion Enclosure Player Guesses Cheese
  *say
    [sfx lion_tree_speech5_without_question.mp3]
    //{empty_string}
  *then
    -> Episode1-Lion Enclosure Ask Player What to Feed Panda
    
@Episode1-Lion Enclosure Player Guesses Bamboo
  *say
    [sfx lion_tree_speech3_fixed.mp3]
    //{empty_string}
  *then
    set context as 'Episode1 - Lion Wall'
    -> Episode1-Player is Prompted to Activate Rope
    
@Episode1-Player is Prompted to Activate Rope
  *say
    The lions are closing in on us. To activate the rope, just say activate rope. Hurry Ranger.

  *reprompt
    This is no time to play games, Ranger. We are surrounded by hungry Lions. You need to use your ranger pack to get us out of here. Say, Activate Rope. Hurry!
  *recap
    This is no time to play games, Ranger. We are surrounded by hungry Lions. You need to use your ranger pack to get us out of here. Say, Activate Rope. Hurry!
  *then
    hear activate rope, rope, activate ranger pack rope, activate ranger pack, use the rope, throw the rope, activate {
      -> Episode1-Lion Enclosure Player Activates Rope
    }
@Episode1-Lion Enclosure Player Guesses Mushroom
  *say
    [sfx lion_tree_speech6_without_question.mp3]
    //{empty_string}
  *then
    -> Episode1-Lion Enclosure Ask Player What to Feed Panda

@Episode1-Lion Enclosure Player Activates Rope
  *say
    [sfx alice_open.mp3]
    Activating ranger pack.
    [sfx activate_rescue_pack.mp3]
    Launching rope.
    [sfx rope_launch_sound.mp3]
    Rope is secure and ready to climb, Ranger.
    [sfx climb_speech3_without_wwyltd.mp3]
  *then
    //used to decide if climb game variables should be initialized
    unflag climb_game_active
    -> Episode1-Lion Enclosure Prompt Player to Climb
    
@Episode1-Lion Enclosure Prompt Player to Climb
  *say
    [sfx wwyltd.mp3]
    //[sfx offer_help1.mp3]
  *then
    hear climb, climb the rope, climb up, use the rope, go up, keep going up, continue going up, keep climbing, keep going up {
      if !climb_game_active {
        //set up variables for the climb game
        unflag panda_hungry
        unflag panda_bored
        set climb_step as 0
        set height as 50
        flag climb_game_active
      }
      
      -> Episode1-Update Climb Height
    }
    hear feed panda, feed the baby panda, feed the baby, give baby panda food, give panda food, give him some food, give him bamboo, feed the baby panda bamboo, feed the baby bamboo, feed him bamboo, try giving him bamboo {
      -> Episode1-Lion Wall Player Feeds Panda
    }
    hear play with panda, give panda toy, play with baby panda, play, give panda toy, give the baby panda a toy to play with, play with him, give him a toy, distract the panda, entertain the panda, entertain him, distract him {
      -> Episode1-Lion Wall Player Plays with Panda
    }
    
@Episode1-Update Climb Height
  *then
    decrease height by 10
    increase climb_step by 1
    -> Episode1-Lion Wall Player Climbs the Rope
    
@Episode1-Lion Wall Player Climbs the Rope
  *say
    [sfx climbing_rope.mp3]
    //50 is the height of the wall in meters
    //10 is the height, in meters, that the player travels upward with each climb command
    We are currently {height} meters from the top of the wall.
  *then
    //Game is over when player makes 5 climb steps
    if (climb_step == 5) {
      set context as 'Episode1 - Top of Lion Wall'
      -> Episode1-Player Completes Climbing Challenge
    }
    else {
      //if statements are so the panda doesn't get bored and hungry at the same time. Cuz that would just be OP.
    
      //If the panda is hungry or bored and the player chose to climb, that means they are risking not feeding or playing with the baby panda. Check if they lose due to that decision.
      //If panda is neither hungry nor bored, then roll the dice in the else statement to see if the panda becomes hungry or bored.
      if (panda_hungry || panda_bored) {
        <-> Episode1-Check if Player Loses
      }
      else {
        <-> Episode1-Check if Panda Hungry
        if (!panda_hungry) {
          <-> Episode1-Check if Panda Bored
        }
      }
    
      //if the panda is hungry or bored then be sure to warn the player
      if (panda_hungry) {
        <-> Episode1-Tell Player Panda is Hungry
      }
      if (panda_bored) {
        <-> Episode1-Tell Player Panda is Bored
      }

      -> Episode1-Lion Enclosure Prompt Player to Climb
      
    }
     
    
@Episode1-Check if Player Loses
  *then
    //50% chance that player loses climb challenge if they don't feed or play with panda
    roll 1d2
      if rollResult == 1 {
        if (panda_hungry) {
          -> Episode1-Player Loses because Panda Hungry
        }
        else {
          -> Episode1-Player Loses because Panda Bored
        }
      }
      
    >> RETURN
@Episode1-Check if Panda Hungry
  *then
    //33% chance that panda is hungry
    roll 1d3
      if rollResult == 1 {
        flag panda_hungry
      }
      
    >> RETURN
@Episode1-Check if Panda Bored
  *then
    //33% chance that panda is bored
    roll 1d3
      if rollResult == 1 {
        flag panda_bored
      }
      
    >> RETURN

@Episode1-Tell Player Panda is Hungry
  *say
    [sfx baby_panda_hungry.mp3]
    My sensors indicate that the panda may be hungry.
    
  *then
    >> RETURN
    
@Episode1-Tell Player Panda is Bored
  *say
    [sfx baby_panda_bored.mp3]
    My sensors indicate that the panda may be bored.
  *then
    >> RETURN
    
@Episode1-Lion Wall Player Feeds Panda
  *say
    [sfx feeding_panda.mp3]
    Sensors indicate that panda is no longer hungry. Nice going Ranger.
  *then
    unflag panda_hungry
    //if the panda is hungry or bored then be sure to warn the player
      if (panda_hungry) {
        <-> Episode1-Tell Player Panda is Hungry
      }
      if (panda_bored) {
        <-> Episode1-Tell Player Panda is Bored
      }
    -> Episode1-Lion Enclosure Prompt Player to Climb
    
@Episode1-Lion Wall Player Plays with Panda
  *say
    [sfx playing_panda.mp3]
    Sensors indicate that panda is no longer bored. Good job Ranger.
  *then
    unflag panda_bored
    //if the panda is hungry or bored then be sure to warn the player
      if (panda_hungry) {
        <-> Episode1-Tell Player Panda is Hungry
      }
      if (panda_bored) {
        <-> Episode1-Tell Player Panda is Bored
      }s
    -> Episode1-Lion Enclosure Prompt Player to Climb
    
@Episode1-Player Loses Climb Challenge
  *then
    //reset game
    unflag climb_game_active
    -> Episode1-Lion Enclosure Prompt Player to Climb
    
@Episode1-Player Loses because Panda Hungry
  *say
    //Baby panda jumps away bc player did not play with panda
    [sfx climb_speech2_without_wwyltd.mp3]
  *then
    -> Episode1-Player Loses Climb Challenge
    
@Episode1-Player Loses because Panda Bored
  *say
    //Baby panda jumps away bc player did not feed panda
    [sfx climb_speech1_without_wwyltd.mp3]
  *then
    ->Episode1-Player Loses Climb Challenge

@Episode1-Player Completes Climbing Challenge
  *say
    [sfx skydive_speech1.mp3]
    You have received the Level 1 Crazy Climber Badge. Nice job, Ranger.
    [sfx skydive_speech2.mp3]
  *then
    flag badge_level_1_crazy_climber
    -> Episode1-Prompt Player to Activate Parachute
    
@Episode1-Prompt Player to Activate Parachute
  *say
    [sfx wwyltd.mp3]
    [sfx offer_help1.mp3]
  
  *reprompt
    We have jumped off the lion wall and are falling fast. Say, activate ranger pack, or, activate parachute to slow your fall.
  *recap
    We have jumped off the lion wall and are falling fast. Say, activate ranger pack, or, activate parachute to slow your fall.
    
  *then
    hear activate ranger pack, activate parachute, open parachute, activate, chute {
      -> Episode1-Return Panda and Return to HQ
    }
    
@Episode1-Return Panda and Return to HQ
  *say
    [sfx final_speech.mp3]
    [sfx teleportation_sound.mp3]
  *then
    -> Episode1-Conclusion
    
@Episode1-Conclusion
  *say
  
    That's the end of episode 1.
    
    Ranger I am truly impressed. You went to China. Searched the zoo. Crawled through air vents. Battled lions. Climbed walls. Skydived. And you rescued a baby panda and got him back to his mother safely. <break time="0.5s" />
    
    The Rescue Rangers owe you a big thanks. I think you have a great future here. 
    
    <break time="0.2s" />
    There are more episodes coming soon, so don't forget to check back in.
    
    You're officially a Rescue Ranger. <break time="0.2s" /> Until next time. 
    
    <break time="0.5s" />
    
    Over and out.
    
    <break time="2s" />

  *then
    -> start
    
    
// It might be a cool idea to add an End-of-Mission Recap (Tresures collected and Recap of Journey).